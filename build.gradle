plugins {
    id 'java'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.3.1"
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

group 'com.alexrnv'
version '1.0.3'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

shadowJar {
    classifier = ''
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'com.alexrnv.pod.upstream.WgetServer'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['shadow']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'wget-server'
        vcsUrl = 'https://github.com/alex-rnv/wget-server'
        licenses = ['Apache-2.0']
        labels = ['wgets']
        publicDownloadNumbers = true
        version {
            name = project.version //Bintray logical version name
        }
    }
}

dependencies {
    compile 'io.vertx:vertx-core:3.1.+'
    compile 'io.vertx:vertx-unit:3.1.+'
    compile 'io.vertx:vertx-codegen:3.1.+'
    compile 'org.apache.commons:commons-lang3:3.+'
    compile 'commons-io:commons-io:2.+'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'net.javacrumbs.json-unit:json-unit:1.6.+'
}
